provider "azurerm" {
  features {}
  subscription_id = var.subscription_id
}

data "azurerm_client_config" "current" {}
data "azurerm_subscription" "primary" {}

# Create Service Principal
resource "azuread_application" "terraform_app" {
  display_name = "terraform-deployer"
}

resource "azuread_service_principal" "terraform_sp" {
  client_id = azuread_application.terraform_app.client_id
}

resource "azuread_application_password" "terraform_sp_password" {
  application_id = azuread_application.terraform_app.id
  display_name   = "terraform-client-secret"
  start_date     = timestamp()
  end_date       = timeadd(timestamp(), "8760h")

lifecycle {
    ignore_changes = [
      start_date,
      end_date,
    ]
  }
}

# Create Resource Group
resource "azurerm_resource_group" "tfstate" {
  name     = var.resource_group_state
  location = local.location
}

# Random suffix for naming
resource "random_string" "suffix" {
  length  = 6
  upper   = false
  special = false
}

# Create Key Vault
resource "azurerm_key_vault" "statefile" {
  name                        = "tfstate-keyvault-${random_string.suffix.result}"
  location                    = local.location
  resource_group_name         = var.resource_group_state
  tenant_id                   = var.tenant_id
  sku_name                    = "standard"
  purge_protection_enabled    = true
  soft_delete_retention_days  = 90
  rbac_authorization_enabled  = true

  depends_on = [azurerm_role_assignment.terraform_sp_role]
}

# Assign Contributor role to SP
resource "azurerm_role_assignment" "terraform_sp_role" {
  principal_id         = azuread_service_principal.terraform_sp.object_id
  role_definition_name = "Contributor"
  scope                = data.azurerm_subscription.primary.id
  depends_on = [azuread_service_principal.terraform_sp]
}

# Grant SP access to Key Vault
resource "azurerm_role_assignment" "terraform_sp_kv_access" {
  scope                = azurerm_key_vault.statefile.id
  role_definition_name = "Key Vault Administrator"
  principal_id         = azuread_service_principal.terraform_sp.object_id

  depends_on = [azurerm_key_vault.statefile]
}

resource "azurerm_role_assignment" "sp_user_access_admin" {
  principal_id         = azuread_service_principal.terraform_sp.object_id
  role_definition_name = "User Access Administrator"
  scope                = azurerm_key_vault.statefile.id

  depends_on = [azuread_service_principal.terraform_sp]
}

## If Using a Managment Group ##
#resource "azurerm_role_assignment" "terraform_sp_mg_access" {
  #principal_id         = azuread_service_principal.terraform_sp.object_id
  #role_definition_name = "Management Group Contributor"
  #scope                = azurerm_management_group.root.id

  #depends_on = [azuread_service_principal.terraform_sp]
#}

## If SP needs to access the statefile for data type definitions ##
#resource "azurerm_role_assignment" "terraform_sp_storage_access" {
  #scope                = azurerm_storage_account.tfstate.id
  #role_definition_name = "Storage Blob Data Reader"
  #principal_id         = azuread_service_principal.terraform_sp.object_id

  #depends_on = [azurerm_key_vault.statefile]
#}

resource "azurerm_storage_account" "tfstate" {
  name                     = "tfstatestore${random_string.suffix.result}"
  resource_group_name      = var.resource_group_state
  location                 = local.location
  account_tier             = "Standard"
  account_replication_type = "LRS"
  access_tier              = "Hot"

  infrastructure_encryption_enabled = true # Confirm requirements, as changes are distructive
  
  blob_properties {
    versioning_enabled = true
  }
   tags = local.tags
   depends_on = [azurerm_resource_group.tfstate]
}

resource "azurerm_storage_container" "tfstate" {
  name                  = "tfstate"
  storage_account_id    = "/subscriptions/${var.subscription_id}/resourceGroups/${azurerm_resource_group.tfstate.name}/providers/Microsoft.Storage/storageAccounts/${azurerm_storage_account.tfstate.name}"
  container_access_type = "private"

depends_on = [azurerm_storage_account.tfstate]
}

output "sp_client_id" {
  value = azuread_application.terraform_app.client_id
}

output "sp_client_secret" {
  value     = azuread_application_password.terraform_sp_password.value
  sensitive = true
}

output "sp_object_id" {
  value = azuread_service_principal.terraform_sp.object_id
}

output "storage_account_name" {
  value = azurerm_storage_account.tfstate.name
}